plugins {
    id 'com.android.library' apply false
    id 'com.android.application' apply false
    id 'org.jetbrains.kotlin.android' apply false
    id 'com.facebook.react' apply false
    id 'com.automattic.android.publish-to-s3' apply false
}

ext {
    compileSdkVersion = 33
    targetSdkVersion = 33
    minSdkVersion = 24

    appCompatVersion = '1.4.2'
    androidxCoreVersion = '1.10.1'
    reactNativeVersion = '0.71.8'

    // Testing
    jUnitVersion = '4.13.2'

    ndkVersion = "25.2.9519653"
    flipperVersion = '0.176.1'

    willPublishWooCommerceSharedLibrary = findProperty("willPublishWooCommerceSharedLibrary")?.toBoolean() ?: false
    reactNativeLibrariesPublishedVersion = findProperty("reactNativeLibrariesPublishedVersion")
}

ext.reactNativeSubProjectsToPublish = [
    "react-native-async-storage_async-storage",
    "react-native-safe-area-context",
    "react-native-screens"
]

configure (subprojects.findAll { reactNativeSubProjectsToPublish.contains(it.name) }) { reactNativeLibrary ->
    apply plugin: "maven-publish"
    apply plugin: "com.automattic.android.publish-to-s3"

    afterEvaluate {
        afterEvaluate {
            publishing {
                publications {
                    Maven(MavenPublication) {
                        from components.release

                        groupId "com.woocommerce.shared"
                        artifactId reactNativeLibrary.name
                        artifact tasks.named("androidSourcesJar")
                        // version is set by 'publish-to-s3' plugin

                        versionMapping {
                            allVariants {
                                fromResolutionOf("releaseRuntimeClasspath")
                            }
                        }
                    }
                }
           }
        }
    }
}
