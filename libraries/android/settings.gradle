pluginManagement {
    gradle.ext.agpVersion = '7.2.1'
    gradle.ext.kotlinVersion = '1.8.10'

    repositories {
        maven {
            url 'https://a8c-libs.s3.amazonaws.com/android'
            content {
                includeGroup "com.automattic.android"
                includeGroup "com.automattic.android.publish-to-s3"
            }
        }
        gradlePluginPortal()
        google()
    }

    plugins {
        id 'com.android.library' version gradle.ext.agpVersion
        id 'com.android.application' version gradle.ext.agpVersion
        id 'org.jetbrains.kotlin.android' version gradle.ext.kotlinVersion
        id "com.automattic.android.publish-to-s3" version "0.7.0"
    }
}

include(":library")
include(":demo")

if (hasProperty("willPublishBinary") && willPublishBinary.toBoolean() == true) {
    assert file("./library/build/assets/index.android.bundle").exists() : "index.android.bundle is necessary to publish a new version!"
}
